buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net' }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
    }
}
plugins {
    id 'com.github.johnrengelman.shadow' version '6.1.0'
}
apply plugin: 'net.minecraftforge.gradle'

group = 'me.hypherionmc'
version = mod_version

if (System.getenv('BUILD_NUMBER') != null) {
    version += "." + System.getenv('BUILD_NUMBER') + "d"
}

java {
    archivesBaseName = 'SimpleDiscordLink-Forge-1.18.x'
    toolchain.languageVersion = JavaLanguageVersion.of(17)
}

minecraft {
    mappings channel: 'official', version: '1.18'

    // accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        client {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'

            mods {
                sdlink {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'

            mods {
                sdlink {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            args '--mod', 'sdlink', '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')

            mods {
                sdlink {
                    source sourceSets.main
                }
            }
        }
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
    mavenLocal()
    maven { url = "https://maven.firstdarkdev.xyz/releases" }
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.18-38.0.14'
    implementation ("me.hypherionmc:sdlink-lib:${library_ver}")
}

configurations.implementation {
    transitive = false
    exclude group: 'org.apache.commons', module: '.*'
    exclude group: 'org.apache.logging.log4j', module: '.*'
    exclude group: 'shadow.apache.commons', module: '.*'
    exclude group: 'shadow.apache.logging.log4j', module: '.*'
    exclude group: 'javax', module: 'annotation'
    exclude group: 'org.jetbrains', module: 'annotations'
}

jar {
    manifest {
        attributes([
                "Specification-Title"     : "sdlink",
                "Specification-Vendor"    : "HypherionMC",
                "Specification-Version"   : "1", // We are version 1 of ourselves
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : project.jar.archiveVersion,
                "Implementation-Vendor"   : "HypherionMC",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

reobf {
    shadowJar {
        dependencies {
            include(dependency("me.hypherionmc:sdlink-lib:${library_ver}"))
            exclude 'META-INF/versions/9/**'
            exclude 'module-info.class'
        }
        //archivesBaseName = project.archiveBaseName
        classifier ''
    }
}

build.dependsOn reobfShadowJar
tasks.shadowJar.finalizedBy reobfShadowJar
