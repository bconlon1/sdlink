buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net' }
        maven { url = 'https://repo.spongepowered.org/repository/maven-public/' }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
        classpath 'org.spongepowered:mixingradle:0.7-SNAPSHOT'
    }
}
plugins {
    id 'com.github.johnrengelman.shadow' version '6.1.0'
}
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.spongepowered.mixin'

group = 'me.hypherionmc'
version = '1.0'

if (System.getenv('BUILD_NUMBER') != null) {
    version = version + "-dev-" + System.getenv('BUILD_NUMBER')
    jar.archiveName = archivesBaseName + "-dev-" + System.getenv('BUILD_NUMBER') + ".jar"
}

java {
    archivesBaseName = 'SimpleDiscordLink-Forge-1.16.5'
    toolchain.languageVersion = JavaLanguageVersion.of(8)
}

minecraft {
    mappings channel: 'official', version: '1.16.5'

    // accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        client {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            arg "-mixin.config=sdlink.mixins.json"

            mods {
                sdlink {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            arg "-mixin.config=sdlink.mixins.json"

            mods {
                sdlink {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            args '--mod', 'sdlink', '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')

            mods {
                sdlink {
                    source sourceSets.main
                }
            }
        }
    }
}

mixin {
    add sourceSets.main, "sdlink.refmap.json"
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
    mavenLocal()
    maven { url = "https://maven.hypherionmc.com/releases" }
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.16.5-36.2.0'
    implementation ("me.hypherionmc:sdlink-lib:${library_ver}") {
        exclude group: 'org.apache.commons', module: 'commons-lang3'
    }
    annotationProcessor 'org.spongepowered:mixin:0.8:processor'
}

jar {
    manifest {
        attributes([
                "Specification-Title"     : "sdlink",
                "Specification-Vendor"    : "HypherionMC",
                "Specification-Version"   : "1", // We are version 1 of ourselves
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : project.jar.archiveVersion,
                "Implementation-Vendor"   : "HypherionMC",
                "MixinConfigs"            : "sdlink-forge.mixins.json",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

reobf {
    shadowJar {
        dependencies {
            include(dependency("me.hypherionmc:sdlink-lib:${library_ver}"))
            exclude 'META-INF/versions/9/**'
            exclude 'module-info.class'
        }
        //archivesBaseName = project.archiveBaseName
        classifier ''
    }
}

build.dependsOn reobfShadowJar
tasks.shadowJar.finalizedBy reobfShadowJar
